#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	#Область СлужебныеПроцедурыИФункции
	
	Функция СоздатьАктыНаСервере(Период) Экспорт
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Ссылка КАК Договор
		|ПОМЕСТИТЬ ВТ_Договоры
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействияДоговора < &КонецПериода
		|	И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействияДоговора > &НачалоПериода
		|	И НЕ ДоговорыКонтрагентов.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка КАК РеализацияТоваровУслуг
		|ПОМЕСТИТЬ ВТ_Реализации
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	НЕ РеализацияТоваровУслуг.ПометкаУдаления
		|	И РеализацияТоваровУслуг.Проведен
		|	И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Договоры.Договор КАК Договор,
		|	ВЫБОР
		|		КОГДА ВТ_Реализации.РеализацияТоваровУслуг ЕСТЬ NULL
		|			ТОГДА ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
		|		ИНАЧЕ ВТ_Реализации.РеализацияТоваровУслуг.Ссылка
		|	КОНЕЦ КАК РеализацияТоваровУслуг
		|ИЗ
		|	ВТ_Договоры КАК ВТ_Договоры
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Реализации КАК ВТ_Реализации
		|		ПО ВТ_Договоры.Договор = ВТ_Реализации.РеализацияТоваровУслуг.Договор";
		
		Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
		Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
		
		МассивДоговоров = Новый Массив;
		
		Выборка = Запрос.Выполнить().Выбрать(); 
		
		Пока Выборка.Следующий() Цикл  
			ДоговорИРеализация = Новый Структура;
			ДоговорИРеализация.Вставить("Договор", Выборка.Договор);		
			Если ЗначениеЗаполнено(Выборка.РеализацияТоваровУслуг) Тогда  
				ДоговорИРеализация.Вставить("РеализацияТоваровУслуг", Выборка.РеализацияТоваровУслуг);
			Иначе
				ДокументРеализация = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
				ДокументРеализация.Дата = Период.ДатаОкончания;	 
				ДокументРеализация.Комментарий = "Создано процедурой массового создания актов";
				ДокументРеализация.Заполнить(Выборка.Договор);
				ДокументРеализация.ВыполнитьАвтозаполнение();
				Если ДокументРеализация.ПроверитьЗаполнение() Тогда
					ДокументРеализация.Записать(РежимЗаписиДокумента.Проведение);
					ДоговорИРеализация.Вставить("РеализацияТоваровУслуг", ДокументРеализация.Ссылка);
				КонецЕсли;
			КонецЕсли;  
		МассивДоговоров.Добавить(ДоговорИРеализация);	
			
		КонецЦикла;
		
		Возврат МассивДоговоров;
		
	КонецФункции
	
	#КонецОбласти
	
#КонецЕсли