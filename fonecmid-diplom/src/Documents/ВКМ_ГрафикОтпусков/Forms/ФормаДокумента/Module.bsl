#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодсветитьДлительныеОтпуска();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников

&НаКлиенте
Процедура ОтпускаСотрудниковДатаОкончанияПриИзменении(Элемент)
	РасчитатьПродолжительностьОтпусков();
	ПодсветитьДлительныеОтпуска();
КонецПроцедуры

&НаКлиенте
Процедура ОтпускаСотрудниковДатаНачалаПриИзменении(Элемент)
	РасчитатьПродолжительностьОтпусков();
	ПодсветитьДлительныеОтпуска();
КонецПроцедуры

&НаКлиенте
Процедура ОтпускаСотрудниковПриИзменении(Элемент)
	ПодсветитьДлительныеОтпуска();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АнализГрафика(Команда)
	
	ПараметрыФормы = ПоместитьВХранилище();;

	ОткрытьФорму("Документ.ВКМ_ГрафикОтпусков.Форма.АнализГрафика", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РасчитатьПродолжительностьОтпусков()

	ТекущаяСтрока = Элементы.ОтпускаСотрудников.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущаяСтрока.ДатаНачала) И ЗначениеЗаполнено(ТекущаяСтрока.ДатаОкончания) Тогда
		ТекущаяСтрока.ПродолжительностьОтпуска = (ТекущаяСтрока.ДатаОкончания - ТекущаяСтрока.ДатаНачала) / 86400;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодсветитьДлительныеОтпуска()

	Если Объект.ОтпускаСотрудников.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ДлительныеОтпуска = ВычислитьДлительныеОтпуска();

	Если ДлительныеОтпуска.Количество() = 0 Тогда
		Для Каждого ТекСтрока Из Объект.ОтпускаСотрудников Цикл
			ТекСтрока.ПодсветитьСотрудника = Ложь;
		КонецЦикла;
	Иначе

		Для Каждого ТекСтрока Из Объект.ОтпускаСотрудников Цикл
			Если ДлительныеОтпуска.Найти(ТекСтрока.Сотрудник) <> Неопределено Тогда
				ТекСтрока.ПодсветитьСотрудника = Истина;
			Иначе
				ТекСтрока.ПодсветитьСотрудника = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ВычислитьДлительныеОтпуска()

	ТаблицаОтпусков = Объект.ОтпускаСотрудников.Выгрузить( , "Сотрудник, ПродолжительностьОтпуска");
	ТаблицаОтпусков.Свернуть("Сотрудник", "ПродолжительностьОтпуска");
	ДлительныеОтпуска = Новый Массив;
	Для Каждого ТекСтрока Из ТаблицаОтпусков Цикл
		Если ТекСтрока.ПродолжительностьОтпуска > 28 Тогда
			ДлительныеОтпуска.Добавить(ТекСтрока.Сотрудник);
		КонецЕсли;
	КонецЦикла;

	Возврат ДлительныеОтпуска;

КонецФункции

&НаСервере
Функция ПоместитьВХранилище()
	
	ТаблицаОтпусков = Объект.ОтпускаСотрудников.Выгрузить( , "Сотрудник, ДатаНачала, ДатаОкончания");
	
	АдресХранилища = ПоместитьВоВременноеХранилище(ТаблицаОтпусков, УникальныйИдентификатор);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Адрес", АдресХранилища);
	ПараметрыФормы.Вставить("Дата", Объект.Дата);
	
	Возврат ПараметрыФормы;
	
КонецФункции


#КонецОбласти